-- programs ------------------------------------------------
entrypoints Program ;
Program.   Program ::= [TopDef] ;
FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;
separator nonempty TopDef "" ;
ArgNoRef.  Arg ::= Type Ident ;
ArgRef.    Arg ::= Type "&" Ident ; 
separator  Arg "," ;

-- statements ----------------------------------------------
Block.     Block ::= "{" [Stmt] "}" ;
separator  Stmt "" ;
Empty.     Stmt ::= ";" ;
BStmt.     Stmt ::= Block ;
Decl.      Stmt ::= Type [Item] ";" ;
ArrDecl.   Stmt ::= Type ArrExpr ;
NoInit.    Item ::= Ident ; 
Init.      Item ::= Ident "=" Expr ;
separator nonempty Item "," ;
Ass.       Stmt ::= Ident "=" Expr  ";" ;
ArrAss.    Stmt ::= ArrExpr "=" Expr ";" ;
Incr.      Stmt ::= Ident "++"  ";" ;
Decr.      Stmt ::= Ident "--"  ";" ;
Ret.       Stmt ::= "return" Expr ";" ;
VRet.      Stmt ::= "return" ";" ;
Cond.      Stmt ::= "if" "(" Expr ")" Block  ;
CondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block ;
While.     Stmt ::= "while" "(" Expr ")" Stmt ;
SExp.      Stmt ::= Expr  ";" ;
Break.     Stmt ::= "break" ;
Continue.  Stmt ::= "continue" ;
FnNestDef. Stmt ::= TopDef ";" ;
SPrint.    Stmt  ::= "print" "(" Expr ")" ";" ;

-- Types ---------------------------------------------------
Int.       Type ::= "int" ;
Str.       Type ::= "string" ;
Bool.      Type ::= "boolean" ;
Void.      Type ::= "void" ;
Function.  Type ::= "function" "<" Type "(" [Type] ")" ">" ; 
internal   Fun. Type ::= Type "(" [Type] ")" ;
separator  Type "," ;

-- Arrays --------------------------------------------------
FirstDim.  ArrExpr ::= Ident "[" Expr "]" ;
MultDim.   ArrExpr ::= ArrExpr "[" Expr "]" ;

-- Expressions ---------------------------------------------
EVar.      Expr6 ::= Ident ;
ELitInt.   Expr6 ::= Integer ;
ELitTrue.  Expr6 ::= "true" ;
ELitFalse. Expr6 ::= "false" ;
EApp.      Expr6 ::= Ident "(" [Expr] ")" ;
EString.   Expr6 ::= String ;
EArr.      Expr6 ::= ArrExpr ;
Neg.       Expr5 ::= "-" Expr6 ;
Not.       Expr5 ::= "!" Expr6 ;
EMul.      Expr4 ::= Expr4 MulOp Expr5 ;
EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;
ERel.      Expr2 ::= Expr2 RelOp Expr3 ;
EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;
EOr.       Expr ::= Expr1 "||" Expr ;
ELambda.   Expr ::= Lambda ;
coercions  Expr 6 ;
separator  Expr "," ;

-- Lambdas -------------------------------------------------
LambdaDef. Lambda ::= "lambda" "<" Type "(" [Arg] ")" ">" Block ;

-- operators -----------------------------------------------
Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------
comment    "//" ;